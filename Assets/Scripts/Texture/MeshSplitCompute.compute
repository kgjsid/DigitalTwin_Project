#pragma kernel CSMain

// Cs 파일의 구조체와 연결(TerrainMeshSplitter.TriangleData)
struct TriangleData
{
    float3 vertex0;
    float3 vertex1;
    float3 vertex2;
    float2 uv0;
    float2 uv1;
    float2 uv2;
    float3 normal0;
    float3 normal1;
    float3 normal2;
    float4 tangent0;
    float4 tangent1;
    float4 tangent2;
};

int _indexStart;                    // 처리할 SubMeshIndex의 시작점(TriangleIndex) 
int _triangleCount;                 // 처리할 삼각형의 총 개수

StructuredBuffer<float3> _vertex;   // 정점
StructuredBuffer<float2> _uv;       // UV 값
StructuredBuffer<float3> _normal;   // 노말벡터
StructuredBuffer<float4> _tangents; // 탄젠트 벡터
StructuredBuffer<int> _triangle;    // 인덱스 배열

// RW : Read, Write 읽고 쓰기 가능한 버퍼
RWStructuredBuffer<TriangleData> _outTriangles;

[numthreads(64, 1, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    // 삼각형 정보 0 ~ 63.
    uint triId = id.x;

    // 예외처리. 만약 삼각형의 수보다 더 큰 스레드를 실행시키면, 해당 스레드는 실행하지 않음
    // ex) 64개의 단위로 실행됨.(64, 1, 1)
    // 100개의 수라면, 128개를 실행하면 그보다 넘는 경우 예외처리 필요
    if (triId >= _triangleCount)
        return;

    // 3개씩 끊어서 삼각형 정보 저장 
    int triangleIndex = _indexStart + int(triId) * 3;

    int i0 = _triangle[triangleIndex + 0];
    int i1 = _triangle[triangleIndex + 1];
    int i2 = _triangle[triangleIndex + 2];

    float3 v0 = _vertex[i0];
    float3 v1 = _vertex[i1];
    float3 v2 = _vertex[i2];

    float2 u0 = frac(_uv[i0]);
    float2 u1 = frac(_uv[i1]);
    float2 u2 = frac(_uv[i2]);

    float3 n0 = _normal[i0];
    float3 n1 = _normal[i1];
    float3 n2 = _normal[i2];

    float4 t0 = _tangents[i0];
    float4 t1 = _tangents[i1];
    float4 t2 = _tangents[i2];

    _outTriangles[triId].vertex0 = v0;
    _outTriangles[triId].vertex1 = v1;
    _outTriangles[triId].vertex2 = v2;
    _outTriangles[triId].uv0 = u0;
    _outTriangles[triId].uv1 = u1;
    _outTriangles[triId].uv2 = u2;
    _outTriangles[triId].normal0 = n0;
    _outTriangles[triId].normal1 = n1;
    _outTriangles[triId].normal2 = n2;
    _outTriangles[triId].tangent0 = t0;
    _outTriangles[triId].tangent1 = t1;
    _outTriangles[triId].tangent2 = t2;
}